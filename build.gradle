apply plugin: 'java'
apply plugin: 'wrapper'

repositories {
   jcenter()
}

compileJava   {
    sourceCompatibility = '11'
    targetCompatibility = '11'
}

dependencies {
   compile 'com.prowidesoftware:pw-swift-core:SRU2019-8.0.1' // parse/write MT940 messages
   compile 'com.opencsv:opencsv:4.0' // csv parsing
   compile 'info.picocli:picocli:4.2.0' // command line parsing

   testImplementation('org.junit.jupiter:junit-jupiter:5.6.0')
}

jar {
    manifest {
        attributes(
                'Main-Class': 'com.github.yuri256.gnucashscripts.cli.Dispatcher'
        )
    }
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
 }

task deploy {
    dependsOn ':deployJar'
    dependsOn ':deployScript'
    dependsOn ':deployConfigExample'
}

task deployJar(type: Copy) {
    dependsOn jar
    from file("$buildDir/libs/${project.name}.jar")
    into file("$System.env.HOME/bin/")
}

task deployScript(type: Copy) {
    from file("$projectDir/deployment/gnuCashScripts.sh")
    into file("$System.env.HOME/bin")
    fileMode 0755
}

task deployConfigExample(type: Copy) {
    from file("$projectDir/conf/gnuCashScripts.conf.example")
    into file("$System.env.HOME/.config/gnuCashScripts")
}
