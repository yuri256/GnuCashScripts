apply plugin: 'java'
apply plugin: 'wrapper'

repositories {
    jcenter()
}

compileJava {
    sourceCompatibility = '11'
    targetCompatibility = '11'
}

dependencies {
    implementation 'com.prowidesoftware:pw-swift-core:SRU2019-8.0.1' // parse/write MT940 messages
    implementation 'com.opencsv:opencsv:4.0' // csv parsing
    implementation 'info.picocli:picocli:4.2.0' // command line parsing

    testImplementation('org.junit.jupiter:junit-jupiter:5.6.0')
}

jar {
    manifest {
        attributes(
                'Main-Class': 'com.github.yuri256.gnucashscripts.cli.Dispatcher'
        )
    }
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    // TODO fix later
    duplicatesStrategy DuplicatesStrategy.WARN
}

tasks.register("deploy") {
    dependsOn ':deployJar'
    dependsOn ':deployScript'
    dependsOn ':deployConfigExample'
}

tasks.register("deployJar", Copy) {
    dependsOn jar
    from file("$buildDir/libs/${project.name}.jar")
    into file("${System.properties.'os.name'.toLowerCase().contains("win") ? System.env.USERPROFILE : System.env.HOME}/bin/")
}

tasks.register("deployScript", Copy) {
    from file("$projectDir/deployment")
    into file("${System.properties.'os.name'.toLowerCase().contains("win") ? System.env.USERPROFILE : System.env.HOME}/bin")
    fileMode 0755
}

tasks.register("deployConfigExample", Copy) {
    from file("$projectDir/conf/gnuCashScripts.conf.example")
    into file("${System.properties.'os.name'.toLowerCase().contains("win") ? System.env.USERPROFILE : System.env.HOME}/.config/gnuCashScripts")
}